plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0'
    id "xyz.jpenilla.run-paper" version "2.3.1"
}

group = 'com.veroud'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.google.code.gson:gson:2.10.1'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(21)
}

processResources {
    filesMatching('paper-plugin.yml') {
        expand(version: version)
    }
}

// ShadowJar configuration
shadowJar {
    archiveBaseName.set('BOMMinecraft')
    archiveVersion.set('1.0-SNAPSHOT')

    // Relocate dependencies
    relocate 'okhttp3', 'com.veroud.libs.okhttp3'
    relocate 'com.google.gson', 'com.veroud.libs.gson'

    // Exclude unnecessary META-INF files
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    mergeServiceFiles()
    minimize()
}


// Make `build` depend on shadowJar
build {
    dependsOn shadowJar
}

// Run server
tasks.runServer {
    minecraftVersion("1.21")
}
